buildscript {
  repositories {
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://repo.spongepowered.org/maven/' }
    maven { url = 'https://plugins.gradle.org/m2/' }
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
    classpath group: 'org.spongepowered',         name: 'mixingradle', version: '0.7-SNAPSHOT'
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

// This initializes the config. Please try to modify the variables in the config file, if needed, add more if you see a reason to.
ext.config = parseConfig(file('build.properties'))

version = config.VERSION
group = "${config.GROUP}.${config.ARTIFACT}"
archivesBaseName = "${config.ARCHIVES_BASE_NAME}-${config.MINECRAFT_VERSION}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
  mappings channel: config.MAPPINGS_CHANNEL, version: config.MAPPINGS_VERSION

  accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

  copyIdeResources = true

  runs {
    configureEach {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'trace'
      property 'forge.enabledGameTestNamespaces', config.MOD_ID
      arg '-mixin.config=questlog.mixins.json'
      jvmArg '-XX:+AllowEnhancedClassRedefinition' // I LOVE DCEVM
      mods {
        "$config.MOD_ID" {
          source sourceSets.main
        }
      }
    }

    client {
    }

    server {
      args '--nogui'
    }

    data {
      workingDirectory project.file('run-data')
      args '--mod', config.MOD_ID, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
    }
  }
}

mixin {
  add sourceSets.main, "questlog.refmap.json"
  config "questlog.mixins.json"

  debug.verbose = true
  debug.export = true
  dumpTargetOnFailure = true
}

configurations {
  packIntoJar {
    // If set to false, any dependencies will not be packed into the jar.
    transitive = false
  }
}

repositories {
  maven { url = 'https://maven.infernalstudios.org/releases' }
}

dependencies {
  minecraft "net.minecraftforge:forge:${config.MINECRAFT_VERSION}-${config.FORGE_VERSION}"
  implementation "org.infernalstudios:config:${config.CONFIG_LIB_VERSION}"
  packIntoJar "org.infernalstudios:config:${config.CONFIG_LIB_VERSION}"
  annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
  archiveClassifier = 'base'
  finalizedBy 'reobfJar'
  manifest {
    attributes([
      'Specification-Title'   : config.TITLE,
      'Specification-Vendor'  : config.AUTHOR,
      'Specification-Version' : '1',
      'Implementation-Title'  : config.TITLE,
      'Implementation-Version': config.VERSION,
      'Implementation-Vendor' : config.AUTHOR,
      'MixinConfigs'          : 'questlog.mixins.json',
    ])
  }
}

shadowJar {
  archiveClassifier = ''
  configurations = [project.configurations.packIntoJar]
  // This is done to avoid any duplicate package issues.
  relocate 'org.infernalstudios.config', "${config.GROUP}.${config.ARTIFACT}.config"
}

reobf {
  shadowJar {}
}

task sourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  from sourceSets.main.allJava
}

tasks.build.dependsOn sourcesJar
tasks.build.dependsOn shadowJar
tasks.build.dependsOn reobfShadowJar

// Config parse function
def parseConfig(File config) {
  config.withReader {
    def prop = new Properties()
    prop.load(it)
    return new ConfigSlurper().parse(prop)
  }
}

if (System.getProperty("idea.sync.active") == "true") {
  afterEvaluate {
    tasks.withType(JavaCompile).all {
      it.options.annotationProcessorPath = files()
    }
  }
}
